#!/usr/bin/perl -w

################################################################
#
# Copyright (c) 1995-2014 SUSE Linux Products GmbH
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License version 2 or 3 as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program (see the file COPYING); if not, write to the
# Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA
#
################################################################

BEGIN {
  unshift @INC, ($::ENV{'BUILD_DIR'} || '/usr/lib/build');
}

use Build;
use strict;

my ($dist, $archs, $configdir, $manifest);

$configdir = ($::ENV{'BUILD_DIR'} || '/usr/lib/build') . '/configs';

while (@ARGV)  {
  if ($ARGV[0] eq '--dist') {
    shift @ARGV;
    $dist = shift @ARGV;
    next;
  }
  if ($ARGV[0] eq '--archpath') {
    shift @ARGV;
    $archs = shift @ARGV;
    next;
  }
  if ($ARGV[0] eq '--configdir') {
    shift @ARGV;
    $configdir = shift @ARGV;
    next;
  }
  if (@ARGV && $ARGV[0] eq '--manifest') {
    shift @ARGV;
    $manifest = shift @ARGV;
    next;
  }
  last;
}

die("usage: order [--manifest manifest] cachedir [packages...]\n") unless @ARGV;
my $cachedir = shift @ARGV;

my @p;
if ($manifest) {
  if ($manifest eq '-') {
    @p = <STDIN>;
  } else {
    open(my $fd, '<', $manifest) || die("$manifest: $!\n");
    @p = <$fd>;
    close $fd;
  }
  chomp @p;
}

push @p, @ARGV;

my $config = Build::read_config_dist($dist, $archs, $configdir);

my %deps;
my %bins;

for my $p (@p) {
  my $q;
  for my $suf ('rpm', 'deb', 'arch', 'apk') {
    next unless -f "$cachedir/$p.$suf";
    if (! -s "$cachedir/$p.$suf") {
      $q = {'provides' => [], 'requires' => []}; # package from preinstallimage, no need to order
      last;
    }
    $q = Build::query("$cachedir/$p.$suf", 'filelist' => 1, 'alldeps' => 1, 'addselfprovides' => 1, 'normalizedeps' => 1);
    die("bad binary: $p.$suf\n") unless $q;
    push @{$q->{'provides'}}, @{$q->{'filelist'}} if $suf eq 'rpm' && $q->{'filelist'};
    delete $q->{'filelist'};
    last;
  }
  die("binary not found: $p\n") unless $q;
  $deps{$p} = $q;
}

Build::readdeps($config, undef, \%deps);
@p = Build::order($config, @p);
print "@p\n";
